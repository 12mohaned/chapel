#!/bin/csh -f
#
#  Application output difference test for ZPL
#
# This is the sub_test that used to appear in all
# subdirectories, but we've now hoisted it to the
# top directory to avoid its over-replication and
# support modifications to it.
#
# The rules:
#
# 1) by default, all subdirectories under the
#    test directory will be run unless they contain
#    a file called NOTEST
#
# 2) if the directory contains a sub_test script,
#    that script will be run.  Otherwise, this one
#    will  be.
#
# 3) if the directory does not contain a sub_test
#    script, the start_test script will look for
#    files named COMPOPTS and EXECOPTS and will
#    add these to the zplcompopts and zplexecopts
#    flags, respectively.


# Check number of incoming arguments
if ( $#argv != 2 ) then
	echo usage\: sub_test compiler testdir
	exit
endif

# commenting this out for now:
## set umask such that other testers can modify files also
#umask 002

set diffargs = "-i"

# make sure can execute compiler

set compiler = "$argv[1]"
set testdir = "$argv[2]"

if ( ! -f $compiler || ! -x $compiler ) then
	echo \[Cannot execute compiler \"$compiler\"\]
	exit
endif

# set variable to location of timedexec, a script that 
#  will only allow a program to execute for a certain number
#  of seconds ($timeout).
if (-r ./TIMEOUT) then
    set globalTimeout = "`cat ./TIMEOUT`"
else
    set globalTimeout = 300
endif
set timedexec = "$testdir/Bin/timedexec"
if ( ! -f $timedexec || ! -x $timedexec ) then
	echo \[Cannot execute timedexec script \"$timedexec\"\]
	exit
endif

# build a string representing the local directory
# strip off $testdir
set sedstr = `echo $testdir | sed 's/\//\\\//g'`
set sedstr = "s/$sedstr\///"
set localdir = `echo $cwd | sed $sedstr`

echo \[Starting subtest - `date`\]
echo \[pwd: \"`pwd`\"\]
# echo \[compiler: \"$compiler\"\]

if (-r ./COMPOPTS) then
    set globalCompopts = "$COMPOPTS `cat ./COMPOPTS`"
else
    set globalCompopts = "$COMPOPTS"
endif

if (-r ./LASTCOMPOPTS) then
    set lastcompopts = `cat ./LASTCOMPOPTS`
else
    set lastcompopts = ""
endif

if (-r ./EXECOPTS) then
    set globalExecopts = "$EXECOPTS `cat ./EXECOPTS`"
else
    set globalExecopts = "$EXECOPTS"
endif

if (-r ./CATFILES) then
    set globalCatfiles = `cat ./CATFILES`
else
    set globalCatfiles = ""
endif

if (-r ./NOEXEC) then
    set execute = "no"
else
    set execute = "yes"
endif

if (-r ./NOVGRBIN) then
    set vgrbin = "no"
else
    set vgrbin = "yes"
endif

if (-r ./COMPSTDIN) then
    set compstdin = "`cat ./COMPSTDIN`"
else
    set compstdin = "/dev/null"
endif

# get all the source files
set testsrc = `ls *.{chpl,v}`

if ($?CHPL_VALGRIND_OPTS == 0) then
  set CHPL_VALGRIND_OPTS = "--tool=memcheck"
endif

if (`uname -a | grep x86_64` == "") then
  set CHPL_VGRND_LINUX32 = "" 
else
  set CHPL_VGRND_LINUX32 = "linux32 " 
endif

if ($CHPLTESTVGRND == "on") then
  set valgrndcomp = "$CHPL_VGRND_LINUX32 valgrind $CHPL_VALGRIND_OPTS --suppressions=$testdir/../compiler/etc/valgrind.suppressions -q "
  if ($vgrbin == "yes") then
    set valgrndbin = "$CHPL_VGRND_LINUX32 valgrind $CHPL_VALGRIND_OPTS -q "
  else
    set valgrndbin = ""
  endif
else
  set valgrndcomp = ""
  set valgrndbin = ""
endif

# compile, execute, and compare each source file
foreach chplsrc ($testsrc)

    # set execfile now since used in both if branches
    set execfile = $chplsrc:r
    set checkfile = $execfile.good
    set complog = $execfile.comp.out.tmp
    set execlog = $execfile.exec.out.tmp

    if (-r ./$execfile.catfiles) then
        set catfiles = "$globalCatfiles `cat ./$execfile.catfiles`"
    else
        set catfiles = "$globalCatfiles"
    endif

    if (-r ./$execfile.compopts) then
	set compopts = "$globalCompopts `cat ./$execfile.compopts`"
    else
        set compopts = "$globalCompopts"
    endif

    if (-r ./$execfile.execopts) then
	set execopts = "$globalExecopts `cat ./$execfile.execopts`"
    else
        set execopts = "$globalExecopts"
    endif

    if (-r ./$execfile.timeout) then
        set timeout = "`cat ./$execfile.timeout`"
    else
        set timeout = "$globalTimeout"
    endif

    if (-r ./$execfile.future) then
	set owner = "`head -n 1 ./$execfile.future`"
	set futuretest = "Future ($owner) "
    else
	set futuretest = ""
    endif

    echo \[Executing: \"$valgrndcomp$compiler $compopts $chplsrc $lastcompopts \< $compstdin\"\]
    $timedexec $timeout "$valgrndcomp$compiler $compopts $chplsrc $lastcompopts" < "$compstdin" >& $complog
	

    if ( $status != 0 || $execute == "no") then
	# Compile unsuccessful
	# Compare compiler output with expected program output
	if ( "$catfiles" != "") then
	    echo \[Concatenating extra files: $catfiles\]
	    cat $catfiles >>& $complog
	endif

	if (-x ./PREDIFF) then
            echo \[Executing PREDIFF\]
	    ./PREDIFF $execfile $complog
	endif
        if (-x ./$execfile.prediff) then
            echo \[Executing $execfile.prediff\]
            ./$execfile.prediff $execfile $complog
        endif

	if ( ! -r $checkfile ) then
	    echo \[Error cannot locate compiler output comparison file $localdir/$checkfile\]
	    continue
	endif

	echo \[Executing diff $checkfile $complog\]
	diff $diffargs $checkfile $complog
	set difffailed = $status
        if ( $difffailed == 0 ) then
	    echo "$futuretest""[Success matching compiler output for $localdir/$execfile]"
	    rm $complog
	else
	    echo "$futuretest""[Error matching compiler output for $localdir/$execfile]"
	    continue
	endif
    else
	# Compile successful
	# Compare output of program execution to expected 
	#   program output
	echo \[Success compiling $localdir/$chplsrc\]

	set binfile = a.out

	if (-x ./PREEXEC) then
            echo \[Executing PREEXEC\]
	    ./PREEXEC $execfile $execlog
	endif
        if (-x ./$execfile.preexec) then
            echo \[Executing $execfile.preexec\]
            ./$execfile.preexec $execfile $complog
        endif


	if ( -f $binfile && -x $binfile ) then
	    # Move compiler output to program output directory.
	    # Will be concatenated with program output so that
	    # compiler warnings and program output are in one
	    # file.

	    mv $complog $execlog

	    if (-r ./$execfile.stdin) then
	        set redirectin = "$execfile.stdin"
	    else 
	        set redirectin = "/dev/null"
	    endif

	    echo \[Executing program $valgrndbin$execfile "$execopts" \< $redirectin at `date +"%H:%M:%S"`\]
	    # Append program output to that of the 
	    # compiler (in case warning were output
	    # by compiler)
	    $timedexec $timeout "$valgrndbin./$binfile $execopts" < $redirectin >>& $execlog

	    if ( $status != 0 ) then
		cat $execlog
		echo \[Error executing program $localdir/$execfile\]
		rm -f $binfile
		continue
	    else
		echo \[Success executing program $localdir/$execfile at `date +"%H:%M:%S"`\]
	    endif

	    if ( "$catfiles" != "") then
		echo \[Concatenating extra files: $catfiles\]
		cat $catfiles >>& $execlog
	    endif

	    if (-x ./PREDIFF) then
                echo \[Executing PREDIFF\]
		./PREDIFF $execfile $execlog
	    endif
            if (-x ./$execfile.prediff) then
                echo \[Executing $execfile.prediff\]
                ./$execfile.prediff $execfile $complog
            endif


	    if ( ! -r $checkfile ) then
		echo \[Error cannot locate program output comparison file $localdir/$checkfile\]
		rm -f $binfile
		continue
	    endif

	    echo \[Executing diff $checkfile $execlog\]
	    diff $diffargs $checkfile $execlog
	    set difffailed = $status
	    if ( $difffailed == 0 ) then
		echo "$futuretest""[Success matching program output for $localdir/$execfile]"
		rm $execlog
		rm -f $binfile
	    else
		echo "$futuretest""[Error matching program output for $localdir/$execfile]"
		rm -f $binfile
		continue
	    endif
	else
	    echo \[Error could not locate executable $localdir/$execfile\]
	endif
    endif
end
		
